' Gambas class file


'Esta clasee sera la que permita darle forma a nuestra serpiente.
'Una serpiente será un conjunto de cuadros que irán pintandose y moviendose por la pantalla


'####################################################

'ESTADO - ATRIBUTOS

'####################################################

'un color para pintarse
'lado en pixeles
'necesitamos poner el cuadrado en un determinado pixel: x,y
Private colorCuadrado As Integer  'Color del cuadrado codificado como número entero.
Private lado As Short 'Se trata del tamaño del cuadrado, del lado, medido en número de pixeles
Private posX As Short  'Posición horizontal de nuestro cuadrado. Será un valor entre 0 y el tamaño horizontal del tablero - 1
Private posY As Short  'Posición vertical de nuestro cuadrado. Será un valor entre 0 y el tamaño vertical del tablero - 1




'####################################################

'COMPORTAMIENTO - MÉTODOS

'####################################################

'Constructor
'primero ponemos public, después sub, _new para los constructores
'después entre paréntesis tantos tripletes de parámetros como atributos (norma general)

Public Sub _new(nuevoColor As Integer, nuevoLado As Short, nuevaX As Short, nuevaY As Short)


      'asignamos los valores
      
      colorCuadrado = nuevoColor
      lado = nuevoLado
      posX = nuevaX
      posY = nuevaY
      
End

'Moverse
Public Sub moverse(letra As String)
  
  If letra = "W" Then 
    'queremos ir parriba
    posY = posY - lado
  Else If letra = "S" Then 
    'queremos ir pabajo
    posY = posY + lado
  
  Else If letra = "A" Then 
    'queremos ir izquierda
    posX = posX - lado

  Else If letra = "D" Then 
    'queremos ir derecha
    posX = lado + posX  
  Endif
  
  
End



'pintarse
Public Sub pintarse(tablero As DrawingArea)
  
    Dim colorLoteria As Integer
    
    colorLoteria = Color.RGB(Rand(200, 255), Rand(0, 50), Rand(0, 255))
  
    Paint.Begin(tablero) 'El objeto Paint es un pintor, y utiliza un lienzo (en este caso nuestro DrawingArea
    Paint.Brush = Paint.Color(colorLoteria) 'Después creamos una brocha con el color de nuestro cuadrado que queremos que sea random
    Paint.Rectangle(posX, posY, lado, lado)  'Pintamos un cuadrado "Rectangle"  
    Paint.Fill 'Rellenamos el interior del cuadrado recién pintado
    Paint.End 'Liberamos el lienzo para que el sistema pueda llevarlo a la pantalla.
  
End


'getters
'Son típicas. Es la manera de "publicar" los valores de mi estado mateniendo la seguridad en el acceso
Public Function getColor() As Integer
  
   Return colorCuadrado
  
End


Public Function getPosX() As Short
  
   Return posX
  
End

Public Function getPosY() As Short
  
   Return posY
  
End


Public Function colisiona(otroCuadrado As Cuadrado) As Boolean
  
  Dim bResultado As Boolean
  
  If (posX = otroCuadrado.getPosX() And posY = otroCuadrado.getPosY()) Then
     bResultado = True
  Else 
    bResultado = False
  Endif
  
  Return bResultado
  
End

